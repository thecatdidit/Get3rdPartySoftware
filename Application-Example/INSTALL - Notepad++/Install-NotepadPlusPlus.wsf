<job id="Install-NotepadPlusPlus">
<script language="VBScript" src="..\..\scripts\ZTIUtility.vbs"/>
<script language="VBScript"> 

' //----------------------------------------------------------------------------
' //
' // Solution: Richard's Deployment Script
' // File: Install-Firefox.wsf
' //
' // Purpose: This will install Notepad++
' //
' // Author: Richard Tracy
' //
' // Usage: cscript Install-NotepadPlusPlus.wsf [/debug:true]
' //
' //----------------------------------------------------------------------------

'//----------------------------------------------------------------------------
'// Global constant and variable declarations
'//---------------------------------------------------------------------------- 

Option Explicit 

Dim iRetVal 

'//----------------------------------------------------------------------------
'// End declarations
'//---------------------------------------------------------------------------- 

'//----------------------------------------------------------------------------
'// Main routine
'//---------------------------------------------------------------------------- 

'On Error Resume Next
iRetVal = ZTIProcess
ProcessResults iRetVal
On Error Goto 0 

'//---------------------------------------------------------------------------
'//
'// Function: ZTIProcess()
'//
'// Input: None
'// 
'// Return: Success - 0
'// Failure - non-zero
'//
'// Purpose: Perform main ZTI processing
'// 
'//---------------------------------------------------------------------------
Function ZTIProcess() 

	Dim sInstallName
	Dim sFile, sInstallerPath, sVersion
	
	sVersion = "7.5.9"
	
	sInstallName = "INSTALL - NOTEPAD++"
	
	oLogging.CreateEntry sInstallName & ": Starting Notepad++ installation", LogTypeInfo	
	
	sFile = "npp." & sVersion & ".Installer.exe"
	sInstallerPath = oUtility.ScriptDir & "\source\" & sFile

	If not oFSO.FileExists(sInstallerPath) then
		oLogging.CreateEntry sInstallName & ": " & sInstallerPath & " was not found, unable to install Notepad++", LogTypeError
		ZTIProcess = Failure
		Exit Function
	End if
	
	'// Disable Zone Checks
	oEnv("SEE_MASK_NOZONECHECKS") = 1 

	'Install Mozilla Firefox
	iRetVal = oShell.Run("""" & sInstallerPath & """ /S", 0, True)
	
	if (iRetVal = 0) or (iRetVal = 3010) then
		ZTIProcess = Success 
		oFSO.CopyFile (oUtility.ScriptDir & "\source\ComparePlugin.dll"), ("C:\Program Files (x86)\Notepad++\plugins\ComparePlugin\ComparePlugin.dll"), True
		oLogging.CreateEntry sInstallName & ": Finished Notepad++ installation", LogTypeInfo
	Else 
		ZTIProcess = Failure
		oLogging.CreateEntry sInstallName & ": Installation failed. Return code from command = " & iRetVal, LogTypeError
	End If
	
	'/// Enable Zone Checks
	oEnv.Remove("SEE_MASK_NOZONECHECKS")

End Function 

Function CopyFile(sName,sDestPath)
	If oFSO.FileExists(oUtility.ScriptDir & "\" & sName) Then
		If Not oFSO.FolderExists(sDestPath) Then
			oLogging.CreateEntry "Created Directory: " & sDestPath, LogTypeInfo
			oFSO.CreateFolder sDestPath
		End If
		oLogging.CreateEntry "Preparing to copy " & oUtility.ScriptDir & "\" & sName, LogTypeInfo
		oFSO.CopyFile oUtility.ScriptDir  & "\" & sName, sDestPath, True
		oLogging.CreateEntry "The file " & sName & " was copied to " & sDestPath, LogTypeInfo
		CopyFile=0
	Else
		oLogging.CreateEntry "Failed to find : " & oUtility.ScriptDir  & "\" & sName, LogTypeERROR
		CopyFile=2
	End If

End Function

</script>
</job>
















